buildscript {
    repositories {
        gradlePluginPortal()
        google()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}
apply from: 'ext.gradle'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'application'

group 'net.ambitious.daigoapi'
version '1.0.0'
mainClassName = "net.ambitious.daigoapi.ApplicationKt"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testResources']
}

repositories {
    gradlePluginPortal()
    google()
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'net.ambitious.daigoapi.ApplicationKt'
    }
}

dependencies {
    def ktor_version = captureVersion(implementation('io.ktor:ktor-server-netty:2.0.2'))
    implementation "io.ktor:ktor-server-locations:$ktor_version"
    implementation "io.ktor:ktor-server-auth:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-jackson:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
    implementation 'com.google.cloud:google-cloud-firestore:3.2.0'
    implementation 'com.google.firebase:firebase-admin:8.2.0'
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.0'

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.json:json:20220320"
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

kotlin.experimental.coroutines = 'enable'

task stage {
    dependsOn assemble
}