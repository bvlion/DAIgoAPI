buildscript {
    repositories {
        gradlePluginPortal()
        google()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.10"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:8.0.0"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'application'

group 'net.ambitious.daigoapi'
version '1.0.0'
mainClassName = "net.ambitious.daigoapi.ApplicationKt"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testResources']
}

repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
}

configurations {
    ktlint
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'net.ambitious.daigoapi.ApplicationKt'
    }
    mergeServiceFiles()
}

dependencies {
    def ktor_version = captureVersion(implementation('io.ktor:ktor-server-netty:2.2.4'))
    implementation "io.ktor:ktor-server-locations:$ktor_version"
    implementation "io.ktor:ktor-server-auth:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-jackson:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation 'ch.qos.logback:logback-classic:1.4.6'
    implementation 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
    implementation 'com.google.cloud:google-cloud-firestore:3.10.0'
    implementation 'com.google.firebase:firebase-admin:9.1.1'
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.0'

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.json:json:20230227"
    ktlint('com.pinterest:ktlint:0.48.2') {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

task stage {
    dependsOn assemble
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "src/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "src/*.kt"
}